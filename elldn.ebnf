
Value    ::= Struct | Array | List | String | Number | Boolean | Null | Symbol | Keyword | Type | Object

Struct   ::= '{' (Key ':'? Value  ( ','? Key ':'? Value)*) '}'
Key      ::= String | Symbol

Object   ::= '#' Type Value

Array    ::= '[' Value (','? Value)* ']'

List     ::= '(' Value (','? Value)* ')'

String    ::= '"' ('Printable UNICODE character except " or \\' | ('\\' ('"' | '\\' | '/' | 'b' | 'f' | 'n' | 'r' | 't' | ('u' Hex Hex Hex Hex))))* '"'
Hex       ::= [0-9a-fA-F]

Number    ::= '-'? ('0' | (NonZeroDigit Digit*) ( '.' Digit+)?) (('e' | 'E') ('+' | '-')? Digit+)?
Digit     ::= [0-9]   
NonZeroDigit   ::= [1-9]   

Boolean   ::= 'true' | 'false'

Null      ::= 'null'

Symbol    ::= [a-zA-Z_#]([a-zA-Z0-9_#!?] | '-')*
Keyword   ::= Symbol ':'
Type      ::= '<' Symbol '>'




Value    ::= Null | Boolean | Number | String | Vector | Struct | List | Symbol | Keyword | Type | Instance

Null      ::= 'null'

Boolean   ::= 'true' | 'false'

Number    ::= '-'? ('0' | (NonZeroDigit Digit*) ( '.' Digit+)?) (('e' | 'E') ('+' | '-')? Digit+)?
Digit     ::= [0-9]   
NonZeroDigit   ::= [1-9]   

String    ::= '"' ('Printable UNICODE character except " or \\' | ('\\' ('"' | '\\' | '/' | 'b' | 'f' | 'n' | 'r' | 't' | ('u' Hex Hex Hex Hex))))* '"'
Hex       ::= [0-9a-fA-F]

Vector    ::= '[' Value (','? Value)* ']'

List     ::= '(' Value (','? Value)* ')'

Struct   ::= '{' (Key ':'? Value  ( ','? Key ':'? Value)*) '}'
Key      ::= String | Symbol

Symbol    ::= [a-zA-Z_#]([a-zA-Z0-9_#!?] | '-')*

Keyword   ::= Symbol ':'

Type      ::= '<' Symbol '>'

Instance   ::= '#' Type Value
